parser

    //Note: parsers don't use keep/noKeep, just  read/look, stack/noStack, and node/noNode.
    attribute defaults "stack" "read" "noNode".    

    keywords 
         "stack" "noStack" "read" "look" "node" "noNode" "keep" "noKeep" 
         "parser" "scanner" "super" "superScanner" 
         "attribute" "defaults" "keywords" "output" "optimize".

    output "All". //One of All, Smalltalk, Ruby, C++, C++98, or Java
   //optimize "chain reductions" "keep nonterminal transitions".

   //NOTE: parser without quotes is a keyword; parser with quotes is a string 
   //which will be reinterpreted as a keyword when the tables actually run
   //because it is output in the tables as a unique string; i.e., a symbol
   //which looks like an identifier.

    Grammar {EndOfFile}
        //Note that #processAndDiscardDefaultsNow eliminates the walkKeywords:, walkAttributeDefaults:, 
        //walkOutput:, and walkOptimize: trees.
        -> GrammarType (ProcessedDefault [noNode])* Rules .

    GrammarType
        //Note that NO tree is built for a grammar type.
        -> "parser" #processTypeNow: [#parser] 
        -> "scanner" #processTypeNow: [#scanner] 
        -> "super" "scanner" #processTypeNow: [#superScanner] 
        -> "superScanner" #processTypeNow: [#superScanner] . 
    
    ProcessedDefault
	-> Default #processDefaultNow .

    Default
        -> "attribute" "defaults" Name+ "." => walkAttributeDefaults: 
        -> "keywords" Name+ "." => walkKeywords: 
        -> "output" Name "." => walkOutput:  
        -> "optimize" Name+ "." => walkOptimize: . 

    Rules
        -> (Production | Macro)+ => walkGrammar: .
    Macro
        -> Name "=" Alternation "." => walkMacro: .

    Production
        -> LeftPart RightParts "." => walkProduction: .

    LeftPart
        -> Name => walkLeftPart:
        -> Name "{" Alternation "}" => walkLeftPartWithLookahead: .

    RightParts -> RightPart+ => walkOr: .

    RightPart
        -> "->" Alternation 
        -> "->" Alternation "=>" TreeBuildingOptions => walkConcatenation: .

    Alternation
        -> => walkPoof:
        -> Concatenation
        -> Concatenation ("|" Concatenation)+ => walkOr: .

    Concatenation
        -> RepetitionOption
        -> RepetitionOption RepetitionOption+ => walkConcatenation: .

    RepetitionOption
        -> Expression
        -> Expression "*" => walkStar:
        -> Expression "?" => walkQuestionMark:
        -> Expression "+" => walkPlus:
        -> Expression "&" Expression => walkAnd: 
        -> Expression "-" Expression => walkMinus: .

    Expression
        -> Primary
        -> SemanticAction => walkNonTreeBuildingSemanticAction:
        -> Primary "[" Attribute [node]* "]" => walkAttributes: .

    Primary
        -> "(" Alternation ")"
        -> "{" Alternation "}" => walkLook: 
        -> (Name | Byte) 
        -> Byte ".." Byte => walkDotDot:. //For scanners only

    TreeBuildingOptions
        -> Name => walkBuildTreeOrTokenFromName: //Tree if parser, Token if scanner
        -> "-" walkInteger: [node] => walkBuildTreeFromRightIndex: //Only for parsers.
        -> "+"? walkInteger: [node] => walkBuildTreeFromLeftIndex: //Only for parsers.
        -> SemanticAction => walkTreeBuildingSemanticAction: .

    Name
        -> walkIdentifier: [node] | walkString: [node].

    Byte
        -> walkCharacter: [node] | walkInteger: [node].

    SemanticAction
        -> walkSymbol: [node] => walkSemanticAction:
        -> walkSymbol: [node] "[" SemanticActionParameter* "]" => walkSemanticAction:.

    SemanticActionParameter
        -> Name | Byte | walkSymbol: [node].

    Attribute
        //For parsers only: stack, noStack, node, and noNode; for scanners only: keep and noKeep.
        //Both read and look are for both BUT look is generally indicated by "{a}" rather than "a [look]"
        //and read is generally indicated by not saying it's what it is; e.g., "a" rather than "a [read]".
        -> ("stack" | "noStack" | "read" | "look" | "node"  | "noNode" | "keep" | "noKeep") [node].
    

